plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.paparazzi
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'net.nicbell.materiallists'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.material
    implementation libs.androidx.constraintlayout

    testImplementation libs.junit
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                pom {
                    name = "Material Lists"
                    description = "An Android XML implementation of Material 3 list items."
                    url = "https://github.com/nicbell/material-lists"
                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = 'nicbell'
                            name = 'Nic Bell'
                        }
                    }
                    scm {
                        url = "https://github.com/nicbell/material-lists"
                        connection = "scm:git://github.com/nicbell/material-lists.git"
                    }
                }
            }
        }
        repositories {
            configure(mavenGitHubMaterialLists)
            configure(mavenSonaTypeSnapshots)
            configure(mavenSonaTypeStaging)
        }
    }

    signing {
        useInMemoryPgpKeys(signingKeyId, signingKey, signingKeyPassword)
        sign publishing.publications.release
    }
}